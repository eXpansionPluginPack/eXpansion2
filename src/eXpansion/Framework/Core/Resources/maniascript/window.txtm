#Include "TextLib" as TextLib
#Include "MathLib" as MathLib

***FML_OnInit***
***
    declare CMlFrame Exp_Window <=> (Page.GetFirstChild("Window") as CMlFrame);
    declare CMlQuad Exp_Titlebar <=> (Page.GetFirstChild("Title") as CMlQuad);
    declare CMlLabel Exp_CloseButton <=> (Page.GetFirstChild("Close") as CMlLabel);
    declare CMlLabel Exp_Title <=> (Page.GetFirstChild("TitleText") as CMlLabel);
    declare Exp_CloseWindow = False;
    declare Exp_OpenWindow for LocalUser = True;

    declare Vec2 Exp_Offset = <0., 0.>;
    declare Real Exp_zIndex = 0.;

    declare Ident Exp_activeWindow for LocalUser = Exp_Window.Id;
    declare Vec2[Text] Exp_PositionCache for This = Vec2[Text];
    declare Boolean[Text] Exp_OpenedWindows for This = Boolean[Text];

    declare Boolean Exp_MoveWindow = False;
    declare Integer Exp_lastAction = Now;
    declare Exp_CloseWindowToggle = True;

    Exp_activeWindow = Exp_Window.Id;
    Exp_OpenWindow = True;

    if (! Exp_PositionCache.existskey(Exp_Title.Value) ) {
        Exp_PositionCache[Exp_Title.Value] = Exp_Window.RelativePosition_V3;
    } else {
        Exp_Window.RelativePosition_V3 = Exp_PositionCache[Exp_Title.Value];
    }
     if (! Exp_OpenedWindows.existskey(Exp_Title.Value) ) {
            Exp_OpenedWindows[Exp_Title.Value] = False;
     }


      if (! Exp_OpenedWindows[Exp_Title.Value] ) {
            Exp_OpenedWindows[Exp_Title.Value] = True;
            Exp_Window.Scale = 1.2;
            AnimMgr.Add(Exp_Window, "<frame scale=\"1.\" />", 150, CAnimManager::EAnimManagerEasing::ExpOut);
      }

***

***FML_MouseClick***
***
if (Event.ControlId == "Close") {
    Exp_CloseWindow = True;
    Exp_lastAction = Now;
}

if (Event.ControlId == "Title")  {
    Exp_Offset = Exp_Window.RelativePosition_V3 - <MouseX, MouseY>;
    Exp_MoveWindow = True;
    Exp_activeWindow = Exp_Window.Id;
}
if (Event.ControlId == "WindowBg")  {
    Exp_activeWindow = Exp_Window.Id;
}
***


***FML_Loop***
***

    if (Exp_OpenWindow) {
        if (Exp_activeWindow == Exp_Window.Id) {
           Exp_Window.ZIndex = 1.;
        } else {
           Exp_Window.ZIndex = -1.;
        }
    }

    if (Exp_CloseWindow) {
        if (Exp_CloseWindowToggle) {
            Exp_CloseWindowToggle = False;
            Exp_OpenedWindows[Exp_Title.Value] = False;
            AnimMgr.Add(Exp_Window, "<frame scale=\"0.8\" />", 150, CAnimManager::EAnimManagerEasing::ExpIn);

        }
    }

    if (Exp_OpenWindow && Exp_lastAction + 150 <= Now ) {
        Exp_OpenWindow = False;
        Exp_lastAction = 0;
    }

    if (Exp_CloseWindow && Exp_lastAction + 150 <= Now ) {
        Exp_Window.Hide();
        TriggerPageAction(Exp_CloseButton.DataAttributeGet("action"));
        Exp_CloseWindow = False;
        continue;
    }

    if (MouseLeftButton == True) {
        if (Exp_activeWindow == Exp_Window.Id) {
            Exp_Window.ZIndex = 1.;
        } else {
            Exp_Window.ZIndex = -1.;
        }
    } else {
        Exp_MoveWindow = False;
    }

    if (Exp_MoveWindow) {
            Exp_Window.ZIndex = 1.;
            Exp_Window.RelativePosition_V3 = <MouseX, MouseY> +  Exp_Offset;
            Exp_PositionCache[Exp_Title.Value] = Exp_Window.RelativePosition_V3;
    }
***
